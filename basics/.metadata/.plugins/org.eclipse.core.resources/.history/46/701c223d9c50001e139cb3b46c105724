/*
 * assembly.s
 *
 *  Created on: 11-Sep-2023
 *      Author: naveen
 */


.syntax unified
.cpu cortex-m4
.fpu softvfp
.thumb

.equ AHB1ENR_ADDR, 0x40023830U
.equ GPIOD_MODE_REG_ADDR, 0x40020C00U
.equ GPIOD_ODR_ADDR, 0x40020C14U
.equ ICSR_ADDR, 0xE000ED04U
.equ SYT_RLV_REG_ADDR, 0xE000E014U
.equ SYT_CNT_REG_ADDR, 0xE000E010U
.equ GPIOD_PERI_CLK_ENB_VAL, 0x8U
.equ GPIOD_MODE_SET_VAL, 0x40000000U
.equ GPIOD_MODE_CLR_VAL, 0x3FFFFFFFU
.equ GPIOD_ODR_TOG_VAL, 0x00008000U
.equ SYT_RELOAD_VALUE, 0x0FFFFFU
.equ SYT_CONTROL_VALUE, 0x6U
.equ ICSR_VALUE, 0x10000000U

.extern current_task
.extern next_task
.extern current_sp

.global PendSV_Handler

.align 4
.section .text.PendSV_Handler
.weak PendSV_Handler
.type PendSV_Handler, %function
PendSV_Handler:
		/* Save Context of Previous Task */
		MRS R0, PSP
		STMDB R0!, {R4-R11}
		/* Save the current SP value */
		LDR R1, =current_task
		LDR R2, [R1]
		LDR R1, =current_sp
		MOV R3, #4
		MUL R2, R2, R3
		ADD R1, R1, R2
		STR R0, [R1]

		/* Retrieve the PSP of next task */
		LDR R1, =next_task
		LDR R2, [R1]
		LDR R1, =current_sp
		MOV R3, #4
		MUL R2, R2, R3
		ADD R1, R1, R2
		LDR R0, [R1]

		/* Restore Context of Next Task */
		LDMIA	R0!, {R4-R11}
		MSR PSP, R0

		/* Make current task equal to next task */
		LDR R1, =next_task
		LDR R2, [R1]
		LDR R1, =current_task
		STR R2, [R1]

		/* Exit Handler */
		BX LR
